program = declarations:decls sequence:sequence {Program(:decls,:sequence)};

declarations = '{' declaration*:decls '}' {:decls} | declaration*;
declaration = ws* "VAR" ws var:v ws* ':' ws* type:t ';' ws+ {Declaration(:v,:t)};
type = "INT" {IntType()}
    | '[' type:ind ws+ "ARRAY" ws+ "OF" ws+ type:val ']' {ArrayType(:ind,:val)}
    | '[' type:t ']' {ArrayType(:t,IntType())};

body = assignment
    | test
    | '{' ws* body:b choice+:chs '}' {buildChoice(:b,:chs)}
    | iteration;
choice = ws* 'U' ws+ body:b ws* {:b};
sequence = '{' ws* subsequence*:ss '}' ws* {Sequence(:ss)};
iteration = '{' ws* subsequence+:ss "}*" {Iteration(Sequence(:ss))};
subsequence = body:b ';' ws+ {:b} | sequence:s {:s};

assignment = var:v ws* ":=" expr:e {Assignment(:v,:e)};
expr = functional | arithmetic;

arithmetic = sub:s ws* '+' ws* arithmetic:a {Add(:s,:a)} | sub;
sub = prod:p subsub*:s {buildSub(:p,:s)}; 
subsub = ws* '-' prod:p {:p};
prod = div:d ws* '*' ws* prod:p {Product(:d,:p)} | div;
div = neg:n subdiv*:d {buildDiv(:n,:d)};
subdiv = ws* '/' neg:n {:n};
neg = ws* '-' neg:n {Neg(:n)} | parentheses;
parentheses = ws* '(' ws* arithmetic:a ws* ')' {:a} | ws* operand;

functional = ws* "APP(" var:array ',' ws? operand:index ')' {Apply(:array,:index)}
    | ws* "UPD(" var:array ',' ws? operand:index ',' ws? operand:new_value ')' {Update(:array,:index,:new_value)}; 

test = '(' ws* or:or ws* ") ?" {Test(:or)};
or = and:and ws+ "||" ws+ or:or {Or(:and,:or)} | and;
and = not:not ws+ "&&" ws+ and:and {And(:not,:and)} | not;
not = '~' ws* relation_atom:ra {Not(:ra)} | relation_atom;
relation_atom = arithmetic:a1 ws* "<=" ws* arithmetic:a2 {LesserEquals(:a1,:a2)}
    | arithmetic:a1 ws* ">=" ws* arithmetic:a2 {GreaterEquals(:a1,:a2)}
    | arithmetic:a1 ws* '<' ws* arithmetic:a2 {Lesser(:a1,:a2)}
    | arithmetic:a1 ws* '>' ws* arithmetic:a2 {Greater(:a1,:a2)}
    | arithmetic:a1 ws* "==" ws* arithmetic:a2 {Equals(:a1,:a2)}
    | '(' ws* or:or ws* ')' {:or};

operand = int | var;
var = (letter (letter | digit)*)$v {Var($v)};
int = digit+$d {Int(s2i($d))};
letter = 'a' - 'z' | 'A' - 'Z' | '_';
digit = ('0'-'9');
ws = ' ' | '\t' | '\n' | '\r';