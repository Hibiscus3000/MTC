import MTC/lab13/annotatedNeMo2s; 
import MTC/lab11/check_errors;

export
{
    checkAnnotatedErrors(annotatedProgram) -> string;
}

checkAnnotatedErrors(annotatedProgram : AnnotatedProgram)
{
    precondition = annotatedProgram.precondition;
    postcondition = annotatedProgram.postcondition;
    program = annotatedProgram.program;

    precondDecls = concat(precondition.declarations,getBoundVariablesDecls(precondition.formula));
    programDecls = program.declarations;
    postCondBoundVarsDecls = getBoundVariablesDecls(postcondition.formula);
    checkedDecls = checkDeclarations(concat3(precondDecls,programDecls,postCondBoundVarsDecls));

    redeclarationMsg = checkedDecls.first;
    if ("" != redeclarationMsg)
        redeclarationMsg
    else
    {
        programVariablesDeclsTree = decls2tree(program.declarations);
        declsTree = setTreeValues(programVariablesDeclsTree,tree2pairs(decls2tree(precondition.declarations)));

        checkAnnotatedBody(program.annotatedBody,declsTree,programVariablesDeclsTree) +
        formFormulaErrorsMsg(precondition.formula,declsTree) +
        formFormulaErrorsMsg(postcondition.formula,declsTree);
    }
}

checkAnnotatedBody(annotatedBody,declsTree,programVariablesDeclsTree)
{
    switch(annotatedBody : AnnotatedBody)
    {
        Assignment(varApply,expr) :
        {
            exprErrorsPair = checkExpr(expr,declsTree);
            exprUndeclaredVars = exprErrorsPair.second;
            varApplyErrorsPair = checkOperand(varApply,programVariablesDeclsTree);
            varApplyUndeclaredVariables = varApplyErrorsPair.second;
            typeMismatches = concat(varApplyErrorsPair.first,exprErrorsPair.first);
            (if (0 != length(varApplyUndeclaredVariables))
                formUndeclaredVarsMsg(varApplyUndeclaredVariables,body2s(annotatedBody,""),
                    "Attempt to use undeclared or not program variable","in left side of assignment")
            else "")
            +
            (if (0 != length(exprUndeclaredVars))
                formUndeclaredVarsMsg(exprUndeclaredVars,body2s(annotatedBody,""),
                    "Attempt to use undeclared or bound variable","in right side of assignment")
            else "")
            +
            (if (0 == length(exprUndeclaredVars) && 0 == length(varApplyUndeclaredVariables))
                (if (isComplicated(expr) && !isInt(expr,declsTree))
                    "Right side of \"" + body2s(annotatedBody,"") + "\" is not INT\n"
                else
                    (if (getExprType(varApply,declsTree) != getExprType(expr,declsTree))
                        "Types of right and left sides of assignment \"" + body2s(annotatedBody,"") + "\" do not match\n"
                    else
                        ""))
                + fold(typeMismatches,"",\acc,typeMismatch -> acc + typeMismatch + "\n")
            else "");
        }
        AnnotatedTest() : checkAnnotatedTest(annotatedBody,declsTree,programVariablesDeclsTree);
        AnnotatedSequence(annotatedBodies) : fold(annotatedBodies,"",
            \acc,annBody -> acc + checkAnnotatedBody(annBody,declsTree,programVariablesDeclsTree));
        AnnotatedChoice(annotatedBodies) : fold(annotatedBodies,"",
            \acc,annBody -> acc + checkAnnotatedBody(annBody,declsTree,programVariablesDeclsTree));
        AnnotatedIteration(invariant,sequence) :
            formFormulaErrorsMsg(invariant,declsTree) 
                + checkAnnotatedBody(sequence,declsTree,programVariablesDeclsTree);
    }
}

checkAnnotatedTest(annotatedTest,declsTree,programVariablesDeclsTree)
{
    switch(annotatedTest : AnnotatedTest)
    {
        SimpleTest(relation) : checkTest(annotatedTest,declsTree);
        AnnotatedWhile(invariant,relation,annotatedBody) :
            formFormulaErrorsMsg(invariant,declsTree) + formRelationErrorsMsg(relation,declsTree,"while (",")")
                + checkAnnotatedBody(annotatedBody,declsTree,programVariablesDeclsTree);
        AnnotatedIf(relation,op_if,op_else) :
        {
            formRelationErrorsMsg(relation,declsTree,"if (",")")
            + checkAnnotatedBody(op_if,declsTree,programVariablesDeclsTree)
            + if (1 == length(op_else)) checkAnnotatedBody(op_else[0],declsTree,programVariablesDeclsTree) else "";
        }
    }
}

formFormulaErrorsMsg(formula,declsTree)
{
    formulaValidity = checkFormula(formula,declsTree);
    undeclaredVariables = formulaValidity.second;
    typeMismatches = formulaValidity.first;
    if (0 != length(undeclaredVariables))
        formUndeclaredVarsMsg(undeclaredVariables,"(" + formula2s(formula,1) + ")",
            "Attempt to use undeclared variable","")
    else
        fold(typeMismatches,"",\acc,error -> acc + error + "\n");
}

checkFormula(formula,declsTree)
{
    switch(formula : Formula)
    {
        AEquivalent(f1,f2) : getFormulaValidity(f1,f2,declsTree);
        AImplication(f1,f2) : getFormulaValidity(f1,f2,declsTree);
        AOr(f1,f2) : getFormulaValidity(f1,f2,declsTree);
        AAnd(f1,f2) : getFormulaValidity(f1,f2,declsTree);
        ANot(f) : checkFormula(f,declsTree)
        FormulaAtom() : checkFormulaAtom(formula,declsTree);
    }
}

getFormulaValidity(r1,r2,declsTree)
{
    r1Pair = checkFormula(r1,declsTree);
    r2Pair = checkFormula(r2,declsTree);
    Pair(concat(r1Pair.first,r2Pair.first),concat(r1Pair.second,r2Pair.second));
}

checkFormulaAtom(formulaAtom, declsTree : Tree<Var,Type>) -> Pair<[string],[string]>
{
    switch(formulaAtom : FormulaAtom)
    {
        LesserEquals(a1,a2) : getRelationAtomValidity(a1,a2,declsTree,formulaAtom);
        GreaterEquals(a1,a2) : getRelationAtomValidity(a1,a2,declsTree,formulaAtom);
        Lesser(a1,a2) : getRelationAtomValidity(a1,a2,declsTree,formulaAtom);
        Greater(a1,a2) : getRelationAtomValidity(a1,a2,declsTree,formulaAtom);
        Equals(a1,a2) : getRelationAtomValidity(a1,a2,declsTree,formulaAtom);
        NotEquals(a1,a2) : getRelationAtomValidity(a1,a2,declsTree,formulaAtom);
        Any(declaration,anyFormula) : checkFormula(anyFormula,setTree(declsTree,declaration.var,declaration.type));
        Exists(declaration,existsFormula) :
            checkFormula(existsFormula,setTree(declsTree,declaration.var,declaration.type));
        True() : Pair([""],[""]);
        False() : Pair([""],[""]);
    }
}

decls2tree(declarations)
{
    fold(declarations,makeTree(),\tree,declaration -> setTree(tree,declaration.var,declaration.type));
}

getFreeVarsDeclsTree(precondition)
{
    decls2tree(precondition.declarations);   
}

getBoundVariablesDecls(formula)
{
    switch (formula : Formula) 
    {
        Any(declaration,anyFormula) : concat([declaration],getBoundVariablesDecls(anyFormula));
        Exists(declaration,existsFormula) : concat([declaration],getBoundVariablesDecls(existsFormula));
        AEquivalent(f1,f2) : concat(getBoundVariablesDecls(f1),getBoundVariablesDecls(f2));
        AImplication(f1,f2) : concat(getBoundVariablesDecls(f1),getBoundVariablesDecls(f2));
        AOr(f1,f2) : concat(getBoundVariablesDecls(f1),getBoundVariablesDecls(f2));
        AAnd(f1,f2) : concat(getBoundVariablesDecls(f1),getBoundVariablesDecls(f2));
        ANot(f) : getBoundVariablesDecls(f);
        Lesser(a1,a2) : [];
        LesserEquals(a1,a2) : [];
        Greater(a1,a2) : [];
        GreaterEquals(a1,a2) : [];
        Equals(a1,a2) : [];
        NotEquals(a1,a2) : [];
        True() : [];
        False() : [];
    }
}