import lingo/pegcode/driver;

export 
{
	Expr ::= Add, Int, Product;
    	Product : (e1 : Expr, e2 : Expr);
		Add : (e1 : Expr, e2 : Expr);
		Int : (i : int);
	e2s(e : Expr) -> string;
	calc(e : Expr) -> int;
	PNAlgebraGrammar() -> [PegOp];
	simpleAlgebraGrammar()  -> [PegOp];
}

simpleAlgebraGrammarOp : ref Maybe<[PegOp]> = ref None();
PNAlgebraGrammarOp : ref Maybe<[PegOp]> = ref None();

simpleAlgebraGrammar()
{
	onlyOnce(simpleAlgebraGrammarOp, \ -> {
		compilePegGrammar("#include MTC/lab5/simple_algebra_grammar.lingo");
	});
}

PNAlgebraGrammar()
{
	onlyOnce(PNAlgebraGrammarOp, \ -> {
		compilePegGrammar("#include MTC/lab6/PN_algebra_grammar.lingo");
	});
}

e2sParentheses(e : Expr, underMultiply : bool) -> string
{
	switch (e : Expr)
	{
		Product(e1,e2) :
			e2sParentheses(e1,true) + " * " + e2sParentheses(e2,true);
		Add(e1,e2) :
			if (underMultiply)
				"(" + e2sParentheses(e1,false) + " + " + e2sParentheses(e2,false) + ")"
			else
				e2sParentheses(e1,false) + " + " + e2sParentheses(e2,false);
		Int(i) :
			i2s(i);
	}
}

e2s(e)
{
	e2sParentheses(e,false);
}

calc(e)
{
	switch(e : Expr)
	{
		Product(e1,e2) :
			calc(e1) * calc(e2);
		Add(e1,e2) :
			calc(e1) + calc(e2);
		Int(i) :
			i;
	}
}
